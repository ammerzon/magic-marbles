plugins {
    id "java"
    id "application"
    id "org.openjfx.javafxplugin" version "0.0.9"
    id "org.beryx.jlink" version "2.22.0"
    id "org.openapi.generator" version "5.0.0-beta3"
}

version "0.1"
group "com.ammerzon"

repositories {
    mavenCentral()
    jcenter()
}

javafx {
    version = "15.0.1"
    modules = [ "javafx.controls", "javafx.fxml" ]
}

sourceSets {
    client {
        java {
            srcDir "$buildDir/generated/src/main/java".toString()
        }
    }
    main {
        resources {
            srcDirs = ["src/main/java", "src/main/resources"]
            includes = ["**/*.fxml", "**/*.css", "**/*.properties", "**/*.png"]
        }
    }
}

compileJava {
    source += sourceSets.client.java
}

ext {
    swagger_annotations_version = "1.5.22"
    jackson_version = "2.10.4"
    ws_rs_version = "2.1.1"
}

dependencies {
    implementation("com.airhacks:afterburner.fx:1.7.0")
    implementation("com.jfoenix:jfoenix:9.0.10")
    compile "com.squareup.okhttp3:okhttp:4.9.0"
    compile "com.google.code.findbugs:jsr305:3.0.2"
    compile "com.fasterxml.jackson.core:jackson-core:$jackson_version"
    compile "com.fasterxml.jackson.core:jackson-annotations:$jackson_version"
    compile "com.fasterxml.jackson.core:jackson-databind:$jackson_version"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jackson_version"
    compile "org.openapitools:jackson-databind-nullable:0.2.1"
    compile 'javax.annotation:javax.annotation-api:1.3.2'
    compile "javax.ws.rs:javax.ws.rs-api:$ws_rs_version"
    compile "io.swagger:swagger-annotations:$swagger_annotations_version"
    testCompile("junit:junit:4.13")
}

mainClassName = "com.ammerzon.App"

openApiGenerate {
    generatorName = "java"
    inputSpec = "$rootDir/../server/build/tmp/kapt3/classes/main/META-INF/swagger/magic-marbles-0.1.yml".toString()
    outputDir = "$buildDir/generated".toString()
    apiPackage = "com.ammerzon.api"
    invokerPackage = "com.ammerzon.api.invoker"
    modelPackage = "com.ammerzon.api.model"
    library = "native"
    configOptions = [
            dateLibrary: "java8"
    ]
}

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = "magicMarbles"
    }
}